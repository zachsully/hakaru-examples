# performs a single update of the model for logistic regression
def logisticUpdate( dset array(pair(array(int),int))
                  , params pair(array(real),real)
                  , eta prob
                  , reg prob):
  match params:
  (feature_params,bias):
    gradient_f =
      array i of size(feature_params):  # foreach feature
        summate d from 0 to size(dset): # forall data
          match dset[d]:
	  (features,label):
            dot = summate k from 0 to size(features):
                    features[k] * feature_params[k]
            (label*features[i]) / (1 + exp(label*(dot + bias)))
    gradient_b =
      summate d from 0 to size(dset): # forall data
        match dset[d]:
	(features,label):
          dot = summate k from 0 to size(features):
                  features[k] * feature_params[k]
          label / (1 + exp(label*(dot + bias)))

    ( array i of size(feature_params):
        feature_params[i]
	- (eta*gradient_f[i])
	+ (feature_params[i]*reg)
    , bias - (eta*gradient_b))


logisticUpdate
